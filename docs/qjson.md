# QJSON — JSON + exact numerics + binary blobs + comments

QJSON is a superset of JSON.  Every valid JSON document is valid
QJSON.  The extensions add what JSON lacks for configuration,
financial data, and embedded systems: exact numbers, binary data,
and human-friendly syntax.

## Types

| Type | JSON | QJSON extension | Example |
|------|------|-----------------|---------|
| null | `null` | — | `null` |
| boolean | `true`, `false` | — | `true` |
| number | `3.14` | — | `3.14` |
| string | `"hello"` | — | `"hello"` |
| array | `[1, 2]` | trailing comma | `[1, 2,]` |
| object | `{"a": 1}` | unquoted keys, trailing comma | `{a: 1,}` |
| BigInt | — | `N` suffix | `42N` |
| BigDecimal | — | `M` suffix | `67432.50M` |
| BigFloat | — | `L` suffix | `3.14159265358979L` |
| blob | — | `0j` prefix (JS64) | `0jSGVsbG8` |

## Numbers

Plain numbers are IEEE 754 doubles.  Exact when representable
(integers up to 2^53, binary fractions like 0.5).  Inexact
otherwise (0.1, most decimals).

### Suffixed numbers: N, M, L

A suffix after a numeric literal marks it as exact:

```
42N                  // BigInt — arbitrary precision integer
67432.50M            // BigDecimal — exact base-10 decimal
3.14159265358979L    // BigFloat — arbitrary precision float
```

Lowercase accepted, canonicalized to uppercase on output.
The suffix must not be followed by an alphanumeric character
(`42N` is BigInt, `42Name` is a parse error).

## Blobs: `0j` prefix

Binary data encoded with JS64 — a base-64 encoding that uses
the 64 legal JavaScript identifier characters as its alphabet.

```
0jSGVsbG8          // 5 bytes: "Hello"
0j0012f580deb4     // raw binary (SHA-256 fragment, key material, etc.)
```

### JS64 alphabet

```
$0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ_abcdefghijklmnopqrstuvwxyz
```

64 characters: `$`, `0-9`, `A-Z`, `_`, `a-z`.  Sorted by
ASCII code point.  Each character encodes 6 bits.

### Encoding

JS64 packs bits LSB-first into 6-bit chunks mapped to the
alphabet.  The full JS64 encoding produces a leading `$`
(zero bits).  The `0j` prefix replaces this leading `$`:

| Full JS64 | QJSON literal |
|-----------|---------------|
| `$SGVsbG8` | `0jSGVsbG8` |
| `$AAAA` | `0jAAAA` |

To decode a `0j` literal: prepend `$`, then JS64-decode.
To encode a blob for QJSON: JS64-encode, strip leading `$`,
prepend `0j`.

Note that leading `$` are significant — even though they represent six zero bits, the length of the JS64 encoding defines the size of the blob: `size(blob) = floor(6*length(encoding)/8)`. `0j` is a 0-length (empty) blob.

### Why not base64?

Standard base64 uses `+` and `/` which aren't valid in
identifiers or URLs without escaping.  JS64 uses only
identifier-safe characters.  A JS64 blob is a valid token
in JavaScript, Prolog, SQL, and shell without quoting.

### Why `0j`?

Follows the `0b`/`0o`/`0x` convention for literal prefixes.
`j` for JS64.  Case-insensitive (`0j` = `0J`).  Doesn't
conflict with `${}` template interpolation.

## Comments

```
// line comment (to end of line)

/* block comment */

/* nested /* block */ comments */
```

Block comments nest.  This is intentional — you can comment
out a region that already contains comments.

## Trailing commas

```
[1, 2, 3,]          // OK
{a: 1, b: 2,}       // OK
```

## Unquoted keys

Object keys that are valid identifiers don't need quotes:

```
{
    name: "alice",
    age: 30,
    _internal: true,
}
```

Valid unquoted key: starts with `[a-zA-Z_$]`, followed by
`[a-zA-Z0-9_$]`.

## QJSON type enum (C API)

```c
typedef enum {
    Y8_NULL, Y8_TRUE, Y8_FALSE,
    Y8_NUM,          // IEEE 754 double
    Y8_BIGINT,       // raw string, suffix N
    Y8_BIGDEC,       // raw string, suffix M
    Y8_BIGFLOAT,     // raw string, suffix L
    Y8_BLOB,         // JS64-decoded byte array
    Y8_STRING,
    Y8_ARRAY,
    Y8_OBJECT
} y8_type;
```

## Implementations

| Language | File | Notes |
|----------|------|-------|
| C | `native/y8_qjson.h`, `native/y8_qjson.c` | Canonical. Arena-allocated, zero malloc, 3.5M msg/sec. |
| JavaScript | `src/qjson.js` | ES5-compatible. Node, Bun, Deno, QuickJS, Duktape. |
| Python | `src/qjson.py` | Pure Python 3. |

All three implementations parse and stringify the same format.
The C implementation is the reference.

## Grammar

JSON defines its representation in terms of characters (Unicode
codepoints), leaving the byte encoding up to the implementation,
and notably limiting `\u` escapes to 4 hex digits (BMP only).

QJSON defines its grammar in terms of bytes.  A language may
keep this representation as a string, but QJSON is primarily
about serialization — transport and storage are sequences of
bytes.

```
value     = ws (null | boolean | number | string | blob
               | array | object) ws

null      = 'null'
boolean   = 'false' | 'true'

number    = '-'? digits ('.' digits)? (('e'|'E') ('+'|'-')? digits)?
            ('N'|'n'|'M'|'m'|'L'|'l')?
digits    = [0-9]+

blob      = '0' ('j'|'J') js64*
js64      = [$0-9A-Z_a-z]

string    = '"' character* '"'
character = <any UTF-8 byte sequence except '"' and '\'>
          | '\"' | '\\' | '\/' | '\b' | '\f' | '\n' | '\r' | '\t'
          | '\u' hex hex hex hex
          | '\u{' hex+ '}'
hex       = [0-9A-Fa-f]

array     = '[' (value (',' value)* ','?)? ']'
object    = '{' (pair (',' pair)* ','?)? '}'
pair      = (string | ident) ':' value
ident     = [a-zA-Z_$] [a-zA-Z0-9_$]*

comment   = '//' <to end of line>
          | '/*' (comment | <any>)* '*/'
ws        = (space | tab | newline | comment)*
```

Notes:
- `ws` is implicit between all tokens.
- Block comments nest (`/* outer /* inner */ still */`).
- Trailing commas are permitted in arrays and objects.
- `\u{hex+}` extends JSON's `\uXXXX` to all Unicode codepoints.
- No suffix after a number means plain IEEE 754 double.
- A number with suffix is a distinct type (`42` ≠ `42N` ≠ `42M`).
- `0j` is unambiguous: no legal number has `j`/`J` after `0`.

## Canonical representation

The canonical form is a deterministic byte sequence for each
value.  Goal: `SHA256(canon(x)) == SHA256(canon(y))` iff
`x` and `y` represent the same value.

### Encoding

UTF-8 bytes.  No BOM.

### Whitespace and comments

None.  No spaces, tabs, newlines, or comments.

### null, boolean

`null`, `true`, `false` — lowercase, no variants.

### Number (no suffix)

Plain numbers are IEEE 754 doubles.  `1` and `1.0` are the
same value (same double).

Canonical form follows the ECMAScript `Number.toString()` rules:
the shortest decimal string that round-trips to the same double.

| Value | Canonical | Not canonical |
|-------|-----------|---------------|
| forty-two | `42` | `42.0`, `042`, `4.2e1` |
| one-tenth | `0.1` | `.1`, `0.10` |
| negative zero | `0` | `-0` |
| 10^20 | `100000000000000000000` | `1e20` |
| 10^21 | `1e+21` | `1000000000000000000000` |
| 5 × 10^-7 | `5e-7` | `0.0000005` |

Scientific notation is used when the exponent is ≥ 21 or ≤ -7
(ECMAScript rules).

### BigInt (N suffix)

Canonical: minimal integer, no leading zeros, no `+` sign,
uppercase suffix.

| Value | Canonical | Not canonical |
|-------|-----------|---------------|
| forty-two | `42N` | `042N`, `42n`, `+42N` |
| zero | `0N` | `00N` |

### BigDecimal (M suffix)

Canonical: strip trailing fractional zeros, strip unnecessary
decimal point, no leading zeros (except `0.x`), no `+` sign,
no scientific notation, uppercase suffix.

QuickJS BigDecimal (libbf) normalizes internally —
`0.50m === 0.5m` is `true`.  Same value, same canonical form.

| Value | Canonical | Not canonical |
|-------|-----------|---------------|
| 67432.5 | `67432.5M` | `67432.50M`, `067432.5M` |
| forty-two | `42M` | `42.0M`, `42.00M` |
| one-tenth | `0.1M` | `00.1M`, `0.10M` |

### BigFloat (L suffix)

Same rules as BigDecimal.  Uppercase `L` suffix.

### Blob

`0j` prefix (lowercase), followed by the JS64 body.
JS64 encoding is deterministic for a given byte sequence.

### String

- Delimited by `"`
- Escape only what is required:
  - `\"` and `\\` (must escape)
  - Control characters 0x00–0x1F as `\uXXXX` (lowercase hex)
- Everything else: literal UTF-8 bytes
- No `\/` escape (literal `/` instead)
- No `\u{...}` in output (accepted on input, emitted as
  literal UTF-8)
- No surrogate pairs in output (use literal UTF-8 for
  codepoints above U+FFFF)

No Unicode normalization — bytes are compared raw.  If you
need NFC equivalence, normalize before serialization.

### Array

Elements in order.  No trailing commas.  No whitespace.

```
[1,2,3]
```

### Object

Keys sorted by UTF-8 byte order (ascending).  Always quoted
(even valid identifiers).  No trailing commas.  No whitespace.
No duplicate keys.

```
{"a":1,"b":2}
```

### Type identity

Different type suffixes create different values:

| Expression | Equal? |
|-----------|--------|
| `42` vs `42` | yes (same double) |
| `1` vs `1.0` | yes (same double) |
| `42` vs `42N` | **no** (double ≠ BigInt) |
| `42N` vs `42M` | **no** (BigInt ≠ BigDecimal) |
| `0.5M` vs `0.50M` | yes (same BigDecimal) |
| `"hello"` vs `"hello"` | yes |
| `0jSGVsbG8` vs `0jSGVsbG8` | yes (same bytes) |

### Summary

| Type | Canonical form |
|------|---------------|
| null | `null` |
| boolean | `true` or `false` |
| number | shortest round-trip decimal (ECMAScript rules) |
| BigInt | minimal integer + `N` |
| BigDecimal | normalized decimal + `M` (no trailing zeros) |
| BigFloat | normalized decimal + `L` (no trailing zeros) |
| blob | `0j` + JS64 body |
| string | `"..."` minimal escapes, literal UTF-8 |
| array | `[v,v,v]` |
| object | `{"k":v,"k":v}` sorted keys, quoted |